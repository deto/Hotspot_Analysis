include: "Snakefile_all"
include: "Snakefile_threshold"
include: "Snakefile_hvg"
include: "Snakefile_danb"
include: "Snakefile_pca"
include: "Snakefile_hs"
include: "Snakefile_shuffled"

rule runHotspotPairs:
    input:
        loom=data(data_file),
        latent=rules.SCVI_hvg.output.latent,
        hs_results=rules.runHotspot_hvg.output.results,
    params:
        model='danb',
        fdrThresh=0.01,
        topN=500,
        n_neighbors=30,
        highXMeanCutoff=20,
    output:
        results_z="hotspot/hotspot_pairs_z.txt.gz"
    script: "pipelineScripts/hotspot/runHotspotPairs.py"

rule runHotspotPairs_null:
    input:
        loom=data(data_file),
        latent=rules.SCVI_hvg.output.latent,
        hs_results=rules.runHotspot_hvg.output.results,
    params:
        model='danb',
        fdrThresh=0.01,
        topN=500,
        n_neighbors=30,
        highXMeanCutoff=20,
    output:
        results_lc="hotspot/hotspot_pairs_lc_null.txt.gz",
        results_z="hotspot/hotspot_pairs_z_null.txt.gz"
    script: "pipelineScripts/hotspot/runHotspotPairs_shuffled.py"

rule runRegularPairs:
    input:
        loom=data(data_file),
        hs_results=rules.runHotspotPairs.input.hs_results,
    params:
        model='danb',
        fdrThresh=rules.runHotspotPairs.params.fdrThresh,
        topN=rules.runHotspotPairs.params.topN,
    output:
        results_lc="hotspot/regular_pairs_lc.txt.gz",
    script: "pipelineScripts/hotspot/runRegularPairs.py"

rule extractHotspotModules:
    input:
        results_z=rules.runHotspotPairs.output.results_z,
    params:
        min_cluster_genes=15,
        core_only=True,
        #z_threshold=1.65,
        fdr_threshold=.05,
    output:
        cluster_heatmap="hotspot/module_heatmap.svg",
        cluster_output="hotspot/modules.txt",
        linkage_output="hotspot/linkage.txt",
    script: "pipelineScripts/hotspot/extractHotspotModules.py"

rule computeModuleScores:
    input:
        loom=data("data.loom"),
        latent=rules.SCVI_hvg.output.latent,
        modules=rules.extractHotspotModules.output.cluster_output,
    params:
        n_neighbors=30,
        model="danb",
    output:
        scores="hotspot/module_scores.txt.gz",
    script: "pipelineScripts/hotspot/computeModuleScores.py"

rule vision:
    input:
        loom=data(data_file),
        latent=rules.SCVI_threshold.output.latent,
        proj_positions=rules.SCVI_threshold.output.latent,
        tsne=rules.TSNE_threshold.output.out,
        umap=rules.UMAP_threshold.output.out,
        proteins=data(ab_file),
    output:
        out="vision/vision.rds"
    script: "pipelineScripts/Vision/vision.R"

rule all:
    input:
        rules.UMAP.output.out,
        rules.TSNE.output.out,
        rules.vision.output.out,
        rules.runHotspotPairs.output,
        rules.runHotspot_hvg.output,
        rules.runHotspot_threshold.output,
        rules.runHotspot_shuffled.output,
        rules.runHotspotPairs_null.output,
        rules.extractHotspotModules.output,
        rules.computeModuleScores.output,
