rule select_threshold_large:
    input:
        loom=data(data_file_large),
    params:
        N_CELLS=100,
    output:
        genes="large_rep/genes/threshold.txt"
    script: "pipelineScripts/select_threshold.py"

rule PCA_threshold_large:
    message: "Computing PCA on thresholded genes"
    input:
        loom=data(data_file_large),
        genes=rules.select_threshold_large.output.genes,
    params:
        doStandardize=True,
        doJackstraw=False,
        num_pcs=5,
    output:
        latent="large_rep/pca/pca_threshold.txt",
        components="large_rep/pca/pca_components.txt",
    script: "pipelineScripts/pca/pca_sim.py"

rule runHotspot_k_sensitivity_large:
    input:
        loom=data(data_file_large),
        latent=rules.PCA_threshold_large.output.latent,
    params:
        model='danb',
        n_neighbors='{k}',
        n_cells_min=10,
        weighted_graph=False,
    output:
        results="large_rep/knn_sensitivity/k_{k}/hotspot/hotspot.txt"
    script: "../../pipelineScripts/hotspot/runHotspot.py"

k_values_large = [5, 10, 30, 50, 100, 300, 500, 1000]

rule runHotspot_k_sensitivity_large_all:
    input:
        expand(rules.runHotspot_k_sensitivity_large.output.results, k=k_values_large)
