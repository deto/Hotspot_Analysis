rule select_hvg:
    input:
        loom=data("cd4/data.loom"),
    params:
        lowXcutoff=0.5,
    output:
        genes="genes/hvg.txt"
    script: "../pipelineScripts/select_hvg.R"

rule PCA_hvg:
    message: "Computing PCA on highly-variable genes"
    input:
        loom=data("cd4/data.loom"),
        genes=rules.select_hvg.output.genes,
    output:
        latent="pca/pca_hvg.txt",
    script: "../pipelineScripts/pca/pca.py"

rule TSNE_hvg:
    message: "Computing TSNE - hvg genes"
    input:
        latent=rules.PCA_hvg.output.latent,
    output:
        out="tsne/tsne_hvg.txt",
    script: "../pipelineScripts/tsne/tsne.py"

rule cluster_hvg:
    message: "Creating clusters on PCA-hvg"
    input:
        latent=rules.PCA_hvg.output.latent,
        tsne=rules.TSNE_hvg.output.out,
    params:
        n_neighbors=30,
        resolution=1,
    output:
        cluster_colors="clusters/cluster_colors_hvg.json",
        cluster_plot="clusters/cluster_hvg.png",
        clusters="clusters/clusters_hvg.txt",
    script: "../pipelineScripts/cluster/makeClusters.py"

rule cluster_markers_hvg:
    message: "Finding cluster markers on PCA-hvg"
    input:
        loom=data("cd4/data.loom"),
        clusters=rules.cluster_hvg.output.clusters,
    output:
        out="clusters/markers_hvg.txt.gz",
        out_xlsx="clusters/markers_hvg.xlsx",
    script: "../pipelineScripts/cluster/cluster_de_1vAll_edgeR.R"

rule runHotspot_hvg:
    input:
        loom=data("cd4/data.loom"),
        latent=rules.PCA_hvg.output.latent,
    params:
        model='danb',
        n_neighbors=30,
        n_cells_min=10,
    output:
        results="hotspot/hotspot_pca_hvg.txt"
    script: "../pipelineScripts/hotspot/runHotspot.py"
