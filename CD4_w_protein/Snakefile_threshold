rule select_threshold:
    input:
        loom=data("cd4/data.loom"),
    params:
        N_CELLS=10,
    output:
        genes="genes/threshold.txt"
    script: "../pipelineScripts/select_threshold.py"

rule PCA_threshold:
    message: "Computing PCA on thresholded genes"
    input:
        loom=data("cd4/data.loom"),
        genes=rules.select_threshold.output.genes,
    output:
        latent="pca/pca_threshold.txt",
    script: "../pipelineScripts/pca/pca.py"

rule SCVI_threshold:
    message: "Computing SCVI on thresholded genes"
    input:
        loom=data("cd4/data.loom"),
        genes=rules.select_threshold.output.genes,
    params:
        components=10,
    output:
        latent="scvi/threshold/latent.txt.gz",
        model="scvi/threshold/model.pkl",
    script: "../pipelineScripts/scvi/scviTorch.py"

rule TSNE_threshold:
    message: "Computing TSNE - hvg genes"
    input:
        latent=rules.SCVI_threshold.output.latent,
    output:
        out="tsne/tsne_threshold.txt",
    script: "../pipelineScripts/tsne/tsne.py"

rule UMAP_threshold:
    message: "Computing UMAP"
    input:
        latent=rules.SCVI_threshold.output.latent,
    params:
        n_neighbors=30,
    output:
        out="umap/umap_threshold.txt",
    script: "../pipelineScripts/umap/umap.py"

rule cluster_threshold:
    message: "Creating clusters on PCA-threshold"
    input:
        latent=rules.SCVI_threshold.output.latent,
        tsne=rules.TSNE_threshold.output.out,
    params:
        n_neighbors=30,
        resolution=1,
    output:
        cluster_colors="clusters/cluster_colors_threshold.json",
        cluster_plot="clusters/cluster_threshold.png",
        clusters="clusters/clusters_threshold.txt",
    script: "../pipelineScripts/cluster/makeClusters.py"

rule cluster_markers_threshold:
    message: "Finding cluster markers on PCA-threshold"
    input:
        loom=data("cd4/data.loom"),
        clusters=rules.cluster_threshold.output.clusters,
    output:
        out="clusters/markers_threshold.txt.gz",
        out_xlsx="clusters/markers_threshold.xlsx",
    script: "../pipelineScripts/cluster/cluster_de_1vAll_edgeR.R"

rule runHotspot_threshold:
    input:
        loom=data("cd4/data.loom"),
        latent=rules.SCVI_threshold.output.latent,
    params:
        model='danb',
        n_neighbors=30,
        n_cells_min=10,
    output:
        results="hotspot/hotspot_threshold.txt"
    script: "../pipelineScripts/hotspot/runHotspot.py"

rule runHotspot_threshold_proteins:
    input:
        proteins=data("cd4/ab.txt.gz"),
        latent=rules.SCVI_threshold.output.latent,
    params:
        model='danb',
        n_neighbors=30,
        n_cells_min=10,
    output:
        results="hotspot/hotspot_threshold_proteins.txt"
    script: "../pipelineScripts/hotspot/runHotspotProteins.py"


